#!/usr/bin/riva
MOD Main;

IMP Std, IO;

IMP Util USE Archive, Persist;
IMP Sys USE FileSys;

IMP IO.Terminal USE Out;
IMP Gtk.Gdk;
IMP Gtk.Gtk;
IMP Gtk.Glade;
IMP Gtk.GObject;

DEF ToolbarStyles <- {
	0 IS Gtk.ToolbarStyle.Icons,
	1 IS Gtk.ToolbarStyle.Text,
	2 IS Gtk.ToolbarStyle.Both
};

DEF conf <- Persist.Load("config") | {
	"toolbar-style" IS 0,
	"zoom-level" IS :"fit-none"	
};

DEF ui <- Glade.Loader.Load("mangax.glade", Main);

VAR thumbnailStore <- Gtk.ListStore.Newv(3, [GObject.Type.STRING, Gdk.Pixbuf.GetType(), GObject.Type.STRING]);
ui.thumbnailView:SetModel(thumbnailStore);
ui.thumbnailView:SetTextColumn(0);
ui.thumbnailView:SetPixbufColumn(1);

VAR mainImageWidth, mainImageHeight;
VAR zoomImageWidth <- -1, zoomImageHeight <- -1;
VAR zoomLevel <- conf["zoom-level"];

DEF Quit!() (
	Gtk.Main.Quit();
	Persist.Store("config", conf);
	RECV msg DO Out:writes(msg, "\n");
	RET :false;
);

DEF About!() (
	ui.aboutDialog:Run;
	ui.aboutDialog:Hide;
);

DEF MainWindowConfigure!(window, event) (
	VAR rect <- ui.mainViewport:Allocation;
	mainImageWidth <- rect:Width;
	mainImageHeight <- rect:Height;
	Out:writes("Main image size = ", mainImageWidth, " x ", mainImageHeight, "\n");
	WHEN zoomLevel
	IS :"fit-both" DO (
		zoomImageWidth <- mainImageWidth;
		zoomImageHeight <- mainImageHeight;
	)
	IS :"fit-height" DO (
		zoomImageHeight <- mainImageHeight;
	)
	IS :"fit-width" DO (
		zoomImageWidth <- mainImageWidth;
	);
	RET :false;
);

DEF FlattenDir(base, subdir) (
	VAR file, path;
	VAR files <- ALL FileSys.ListDir(subdir);
	files:remove(".");
	files:remove("..");
	EVERY path <- subdir + "/" + (file <- files:values) DO (
		FileSys.FileType(path):and(FileSys.DIRFILE) ~= 0 => (
			FlattenDir(base, path);
			FileSys.RemoveDir(path);
		) // (
			FileSys.Rename(path, base + "/" + file);
		);
	);
);

DEF FlattenArchive(file) (
	VAR dir <- FileSys.TempFile();
	FileSys.MakeDir(dir, 511);
	Archive.Extract(file, dir);
	FlattenDir(dir, dir);
	RET dir;
);

DEF OpenFile!() (
	thumbnailStore:Clear;
	ui.fileChooserDialog:SetAction(Gtk.FileChooserAction.Open);
	ui.fileChooserDialog:Run = Gtk.ResponseType.Ok => (
		VAR file <- ui.fileChooserDialog:GetFilename;
		VAR dir;
		Archive.Type(file) => (
			dir <- FlattenArchive(file);
		) // (
			dir <- ui.fileChooserDialog:GetCurrentFolder;
		);
		VAR iter <- Gtk.TreeIter.New();
		VAR value <- GObject.Value.New();
		VAR index <- 0;
		VAR file, path;
		EVERY path <- dir + "/" + (file <- Sys.FileSys.ListDir(dir)) DO (
			value:Set(Gdk.Pixbuf.NewFromFileAtSize(path, 64, 128, NIL));
			thumbnailStore:Append(iter);
			thumbnailStore:SetValue(iter, 1, value);
			value:Unset;
			value:Set(path);
			thumbnailStore:SetValue(iter, 2, value);
			value:Unset;
			index <- index + 1;
			value:Set(index @ Std.String.T);
			thumbnailStore:SetValue(iter, 0, value);
			value:Unset;
			RECV msg DO ();
		);
	);
	ui.fileChooserDialog:Hide;
);

DEF OpenFolder!() (
	ui.fileChooserDialog:SetAction(Gtk.FileChooserAction.SelectFolder);
	ui.fileChooserDialog:Run = Gtk.ResponseType.Ok => (
		Out:writes("You selected ", ui.fileChooserDialog:GetFilename, "\n");
	);
);

DEF ThumbnailActivated!(iconview, path) (
	VAR iter <- Gtk.TreeIter.New();
	VAR value <- GObject.Value.New();
	thumbnailStore:GetIter(iter, path);
	thumbnailStore:GetValue(iter, 2, value);
	VAR pixbuf <- Gdk.Pixbuf.NewFromFileAtSize(value:Get, zoomImageWidth, zoomImageHeight, NIL);
	value:Unset;
	ui.mainImage:SetFromPixbuf(pixbuf);
	pixbuf:Unref;
);

DEF PrefsDialogRealize!() (
	ui.toolbarStyleComboBox:SetActive(conf["toolbar-style"]);
);

DEF preferenceChanges <- {};

DEF ApplyPreferences!() (
	EVERY (preferenceChanges:values)();
	preferenceChanges:empty;
);

DEF Preferences!() (
	preferenceChanges:empty;
	ui.prefsDialog:Run = Gtk.ResponseType.Ok => ApplyPreferences();
	ui.prefsDialog:Hide;
);

DEF ToolbarStyleChanged!(combobox) (
	ui.mainToolbar:SetStyle(ToolbarStyles[conf["toolbar-style"] <- combobox:GetActive]);
);

DEF ViewFitNone!() (
	zoomImageWidth <- -1;
	zoomImageHeight <- -1;
	zoomLevel <- :"fit-none";
);

DEF ViewFitScreen!() (
	zoomImageWidth <- mainImageWidth;
	zoomImageHeight <- mainImageHeight;
	zoomLevel <- :"fit-both";
);

DEF ViewFitHeight!() (
	zoomImageWidth <- -1;
	zoomImageHeight <- mainImageHeight;
	zoomLevel <- :"fit-height";
);

DEF ViewFitWidth!() (
	zoomImageWidth <- mainImageWidth;
	zoomImageHeight <- -1;
	zoomLevel <- :"fit-width";
);

DEF ShowNextImage!() (
);

VAR imageMotionHandler() :true;
VAR imageReleaseHandler() :true;

DEF ImageRelease!(widget, event) imageReleaseHandler(event);
DEF ImageMotion!(widget, event) imageMotionHandler(event);

VAR imageDragX;
VAR imageDragY;

DEF ImagePress!(widget, event) (
	event <- event:Button;
	WHEN event:Button
	IS 1 DO (
		imageDragX <- event:XRoot;
		imageDragY <- event:YRoot;
		imageReleaseHandler <- <event> (
			imageReleaseHandler <- <> :true;
		);
		imageMotionHandler <- <event> (
			event <- event:Motion;
			imageReleaseHandler <- <> :true;
			DEF hadjust <- ui.mainScrolledWindow:GetHadjustment;
			DEF vadjust <- ui.mainScrolledWindow:GetVadjustment;
			VAR x <- event:XRoot;
			VAR y <- event:YRoot;
			hadjust:ClampPage(hadjust:GetValue - x + imageDragX, hadjust:Upper);
			vadjust:ClampPage(vadjust:GetValue - y + imageDragY, vadjust:Upper);
			imageDragX <- x;
			imageDragY <- y;
			:true;
		);
	)
	IS 3 DO
		ui.popupMenu:Popup(Gtk.Widget.Nil, Gtk.Widget.Nil, Std.Function.Nil, Std.Address.Nil, 3, event:Time);
	:true;
);

ui.mainToolbar:SetStyle(ToolbarStyles[conf["toolbar-style"]]);
ui.mainWindow:ShowAll;
Gtk.Main.Run();

END Main.
