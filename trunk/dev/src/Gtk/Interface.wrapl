MOD Interface;

IMP Type, Target;
IMP IO.File;
IMP Std.String;

DEF T <- <[Type.T, Target.T]
	ns, name, cname,
	methods,
	ctype, rtype,
	headers, libraries
>;

DEF New!(ns, name, cname) (
	VAR t <- T();
	t:ns <- ns;
	t:name <- name;
	t:cname <- cname;
	t:methods <- [];
	t:rtype <- "Gtk$" + ns + "$" + name + "$T";
	t:ctype <- cname + " *";
	Type.Define(cname + "*", t);
	t:headers <- ["#include <Gtk/" + ns + "/" + name + ".h>"];
	t:libraries <- ["Gtk/" + ns + "/" + name + ".rlib"];
	t:queue;
	RET t;
);

TO :addmethod(t@T, name, cname) (
	VAR method <- Type.Method(name, cname, []);
	method:addparam("self", t:cname + "*");
	t:methods:put(method);
	RET method;
);

TO :precall(t@T, name, arg) [
	t:ctype + " " + name + " = ((Gtk$GObject$Object_t *)" + arg + ".Val)->Handle;"
];

TO :return(t@T, result) [
	"if (" + result + " == 0) return FAILURE;",
	"Result->Val = Gtk$GObject$Object$to_riva(" + result + ");"
];

TO :process(t@T, srcdir, incdir, libdir) (
	VAR base <- t:ns + "/" + t:name;
	VAR cfile <- File.Open(srcdir + base + ".c", File.WRITE + File.TEXT);
	cfile:writes("#include <Gtk/", base, ".h>\n\n");
	cfile:writes("TYPE(T);\n\n");
	VAR method, param, i, returntype, types, names, n, params;
	EVERY method <- t:methods:values DO (
		params <- method:params;
		n <- params:size;
		types <- params:map(<p> Type.Lookup(p:type));
		names <- params:map(:name);
		cfile:writes("METHOD(\"", method:name, "\", TYP, T");
		EVERY cfile:writes(", TYP, ", types[2:to(n)]:rtype);
		cfile:write(") {\n");
		EVERY i <- 1:to(n) DO (
			EVERY cfile:writes("\t", types[i]:precall(names[i], "Args[" + i@String.T + "]"):values, "\n");
		);
		cfile:write("\t");
		returntype <- Type.Lookup(method:type);
		returntype:ctype ~= "void" => cfile:writes(returntype:ctype, " result = ");
		cfile:writes(method:cname, "(self");
		EVERY i <- 2:to(n) DO cfile:writes(", ", names[i]);
		cfile:writes(");\n");
		EVERY i <- 1:to(n) DO (
			EVERY cfile:writes("\t", types[i]:postcall(names[i], "Args[" + i@String.T + "]"):values, "\n");
		);
		returntype:ctype ~= "void" => (
			EVERY cfile:writes("\t", returntype:return("result"):values, "\n");
		);
		cfile:write("\treturn SUCCESS;\n");
		cfile:write("};\n\n");
	);
	cfile:close;
VAR deffile <- File.Open(srcdir + base + ".rdef", File.WRITE + File.TEXT);
	deffile:writes(>>EOF
if WINDOWS then
	_export = export
	export = function(Name)
		_export("_" .. Name, Name)
	end
end

export("T")
EOF);
	deffile:close;
	VAR libfile <- File.Open(libdir + base + ".rlib", File.WRITE + File.TEXT);
	libfile:writes("module(\"Gtk\", \"", t:ns, "\", \"", t:name, "\")\n");
	libfile:writes("prefix(\"Gtk$", t:ns, "$", t:name, "$\")\n");
	libfile:write("export(\"T\")\n");
	libfile:close;
	VAR hfile <- File.Open(incdir + base + ".h", File.WRITE + File.TEXT);
	hfile:writes("#ifndef ", t:ns, "_", t:name, "_H\n");
	hfile:writes("#define ", t:ns, "_", t:name, "_H\n\n");
	hfile:writes("#define RIVA_MODULE Gtk$", t:ns, "$", t:name, "\n");
	hfile:writes("#include <Riva-Header.h>\n\n");
	hfile:writes("extern Std$Type_t ", t:rtype, "[];\n\n");
	hfile:write("#undef RIVA_MODULE\n\n");
	hfile:write("#endif\n");
	hfile:close;
);

END Interface.
