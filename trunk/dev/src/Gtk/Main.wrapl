MOD Main;

IMP Xml.Parser;
IMP IO.Terminal USE Out;
IMP IO.File;

IMP Target;
IMP Callback;
IMP Class;
IMP Enum;
IMP Interface;
IMP Native;
IMP Object;
IMP Struct;

DEF Environment <- <[]
	namespace,
	object,
	method
>;

DEF StartHandlers <- {

"virtual_method" IS <env, attrs> (
	env:method <- env:object:addmethod(attrs["name"], attrs["cname"]);
),

"field" IS <env, attrs> (
),

"return-type" IS <env, attrs> (
	env:method:type <- attrs["type"];
),

"method" IS <env, attrs> (
	env:method <- env:object:addmethod(attrs["name"], attrs["cname"]);
),

"api" IS <env, attrs> (
),

"alias" IS <env, attrs> (
),

"parameters" IS <env, attrs> (
),

"callback" IS <env, attrs> (
	env:method <- Callback.New(env:namespace, attrs["name"], attrs["cname"]);
),

"interface" IS <env, attrs> (
	env:object <- Interface.New(env:namespace, attrs["name"], attrs["cname"]);
),

"class" IS <env, attrs> (
	env:object <- Class.New(env:namespace, attrs["name"], attrs["cname"]);
),

"namespace" IS <env, attrs> (
	env:namespace <- attrs["name"];
),

"object" IS <env, attrs> (
	env:object <- Object.New(env:namespace, attrs["name"], attrs["cname"], attrs["parent"]);
),

"struct" IS <env, attrs> (
	env:object <- Struct.New(env:namespace, attrs["name"], attrs["cname"]);
),

"member" IS <env, attrs> (
	env:object:member(attrs["name"], attrs["cname"]);
),

"constructor" IS <env, attrs> (
	env:method <- env:object:addctor(attrs["cname"]);
),

"enum" IS <env, attrs> (
	env:object <- Enum.New(env:namespace, attrs["name"], attrs["cname"]);
),

"parameter" IS <env, attrs> (
	attrs["ellipsis"] => (
		env:method:addparam("...", "ellipsis");
	) // (
		env:method:addparam(attrs["name"], attrs["type"]);
	);
),

"static-string" IS <env, attrs> (
)

};

VAR source <- File.Open("api.xml", File.READ + File.TEXT);
VAR parser <- Parser.New();
parser:userdata <- Environment();
parser:onstartelement <- <env, name, attrs> StartHandlers[name](env, attrs);

Out:write("Reading api file...");
REP WHILE parser:parse(source:read(1024));
Out:write("done.\n");

Out:write("Generating files...");
Target.Process();
Out:write("done.\n");

END Main.
