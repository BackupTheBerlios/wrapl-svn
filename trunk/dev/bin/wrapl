#!INTERPRETER
MOD Main;

IMP Std.String;
IMP Std.Symbol;
IMP Std.Address;
IMP IO.Terminal USE In, Out;
IMP Wrapl;
IMP Sys.Module;
IMP Sys.Process;
IMP Sys.Config;
IMP Sys.Log;

VAR arg;
EVERY WHEN arg <- Process.Args:values
IS "-u" DO
	Config.Set("Wrapl.AllowUndeclared")
IS "-l" DO
	Log.Enable();
DO
	Out:writes("Warning: unknown argument: ", arg, ".\n")
;

Out:write("Interactive Wrapl Version: " + Wrapl.Version + "\n");

VAR session <- Wrapl.SessionNew(In);
VAR result;

Wrapl.SessionVar(session, "_", result);

REP (
	Out:write("--> ");
	(result <- Wrapl.SessionEval(session)) => (
		Out:write(result);
		RECV msg DO msg IN Symbol.NoMethodMessageT => (
			VAR mod;
			(mod <- Module.FromVal(result)) => (
				Out:writes(mod[1]:map("/", "."), ".", mod[2]);
			) // (mod <- Module.FromVal(?result)) => (
				Out:writes("<", mod[1]:map("/", "."), ".", mod[2], ":", Address.FromVal(result), ">");
			) // (
				Out:write("<value>");
			);
		) // (
			SEND msg;
		);
	) // (
		Out:write("failure");
	);
	RECV msg DO msg IN Wrapl.ErrorMessageT => (
		Out:writes("console", msg);
	) // (
		Out:write("Unhandled message: ");
		Out:write(msg);
		RECV msg2 DO Out:write("<value>");
	);
);

END Main.
