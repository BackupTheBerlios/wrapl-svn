#!/usr/local/bin/riva
MOD Main;

IMP Std.String;
IMP Std.Symbol;
IMP IO.Terminal USE In, Out;
IMP Wrapl;
IMP Sys.Module;

Out:write("Interactive Wrapl Version: " + Wrapl.Version + "\n");

VAR Session <- Wrapl.SessionNew(In);
VAR Result;

REP (
	Out:write("--> ");
	(Result <- Session:eval) => (
		Out:write(Result);
		RECV Msg DO Msg IN Symbol.NoMethodMessageT => (
			VAR mod;
			(mod <- Module.FromVal(Result)) => (
				Out:writes(mod[1]:map("/", "."), ".", mod[2]);
			) // (
				Out:write("<value>");
			);
		) // (
			SEND Msg;
		);
	) // (
		Out:write("failure");
	);
	RECV Msg DO Msg IN Wrapl.ErrorMessageT => (
		Out:write("console");
		Out:write(Msg);
	) // (
		Out:write("Unhandled message: ");
		Out:write(Msg);
		RECV Msg2 DO Out:write("<value>");
	);
);

END Main.
